import "../../customGameSettings.json";

playervar define cursor_distance;

rule: "Setup"
{
    DisableAnnouncer();
    DisableCompletion();
    DisableMusic();
    DisableScoring();
}

rule: "Player: Ultimate Held Retract Cursor"
Event.OngoingPlayer
if (IsButtonHeld(EventPlayer(), Button.Ultimate))
if (!IsButtonHeld(EventPlayer(), Button.Ability2))
{
    ChaseVariableAtRate(cursor_distance, 0, 4, RateChaseReevaluation.None);
}

rule: "Player: Ability 2 Held Retract Cursor"
Event.OngoingPlayer
if (!IsButtonHeld(EventPlayer(), Button.Ultimate))
if (IsButtonHeld(EventPlayer(), Button.Ability2))
{
    ChaseVariableAtRate(cursor_distance, 100, 4, RateChaseReevaluation.None);
}

rule: "Player: Still Cursor"
Event.OngoingPlayer
if (!IsButtonHeld(EventPlayer(), Button.Ultimate))
if (!IsButtonHeld(EventPlayer(), Button.Ability2))
{
    StopChasingVariable(cursor_distance);
}

rule: "Player: Interact Held Speedup"
Event.OngoingPlayer
if (IsButtonHeld(EventPlayer(), Button.Interact))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
{
    SetMoveSpeed(EventPlayer(), 600);
}

rule: "Player: Interact Not Held Crouch Not Held Normal speed"
Event.OngoingPlayer
if (!IsButtonHeld(EventPlayer(), Button.Interact))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
{
    SetMoveSpeed(EventPlayer(), 300);
}

rule: "Player: Crouch Held Slowdown"
Event.OngoingPlayer
if (!IsButtonHeld(EventPlayer(), Button.Interact))
if (IsButtonHeld(EventPlayer(), Button.Crouch))
{
    SetMoveSpeed(EventPlayer(), 30);
}

rule: "Skip Assembly"
if (IsInSetup())
{
    SetMatchTime(0);
}

rule: "Player Setup"
Event.OnPlayerJoin
{
    cursor_distance = 10;
    ForcePlayerHero(EventPlayer(), Hero.Ana);
    //DisableHeroHud(EventPlayer());
    DisableGameModeHud(EventPlayer());
    DisableKillFeed(EventPlayer());
    DisableMessages(EventPlayer());
    DisableSpectate(EventPlayer());
    DisableSpectateHUD(EventPlayer());
    DisableNameplates(EventPlayer());
    DisableRespawning(EventPlayer());
    DisableScoreboard(EventPlayer());
    SetUltimateAbilityEnabled(EventPlayer(), false);
    SetMeleeEnabled(EventPlayer(), false);
    SetReloadEnabled(EventPlayer(), false);
    SetCrouchEnabled(EventPlayer(), false);
    SetAbility1Enabled(EventPlayer(), false);
    SetAbility2Enabled(EventPlayer(), false);
    SetPrimaryFireEnabled(EventPlayer(), false);
    SetSecondaryFireEnabled(EventPlayer(), false);
    SetJumpEnabled(EventPlayer(), true);
    SetDamageReceived(EventPlayer(), 0);

    # Teleport(EventPlayer(), Vector(-143, -3, -100));
    
    SetMoveSpeed(EventPlayer(), 300);
    CreateHudText(
        VisibleTo: EventPlayer(),
        Header: <"Position\nX: <0>\nY: <1>\nZ: <2>", XOf(PositionOf(EventPlayer())), YOf(PositionOf(EventPlayer())), ZOf(PositionOf(EventPlayer()))>,
        Location: Location.Left,
        Reevaluation: HudTextRev.String,
        SortOrder: 1
    );

    CreateHudText(
        VisibleTo: EventPlayer(),
        Header: <"Cursor\nDistance: <3>\nX: <0>\nY: <1>\nZ: <2>", XOf(CURSOR()), YOf(CURSOR()), ZOf(CURSOR()), cursor_distance>,
        TextColor: Color.Blue,
        Location: Location.Right,
        Reevaluation: HudTextRev.String,
        SortOrder: 1
    );

    CreateHudText(
        VisibleTo: EventPlayer(),
        Header: <"Facing\nX: <0>\nY: <1>\nZ: <2>", XOf(EventPlayer().FacingDirection()), YOf(EventPlayer().FacingDirection()), ZOf(EventPlayer().FacingDirection())>,
        TextColor: Color.Blue,
        Location: Location.Right,
        Reevaluation: HudTextRev.String,
        SortOrder: 1
    );

    CreateEffect(
        VisibleTo: EventPlayer(),
        Position: CURSOR(),
        Type: Effect.Orb,
        Color: Color.Blue,
        Reevaluation: EffectRev.PositionAndRadius,
        Radius: 0.25
    );
}

define CURSOR(): RayCastHitPosition(EyePosition(EventPlayer()), EyePosition(EventPlayer()) + cursor_distance * FacingDirectionOf(EventPlayer()));